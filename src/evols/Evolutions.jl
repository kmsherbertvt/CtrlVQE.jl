export trapezoidaltimegrid
export EvolutionType, TrotterEvolution
export evolve, evolve!, workbasis, nsteps, gradientsignals

import ..LinearAlgebraTools
import ..Devices
import ..Bases

import ..Bases: OCCUPATION
import ..Operators: STATIC, Drive, Gradient

import ..TempArrays: array
const LABEL = Symbol(@__MODULE__)

using LinearAlgebra: norm
using Memoization: @memoize


"""
    trapezoidaltimegrid(T::Real, r::Int)

All the tools needed to integrate over time using a simple trapezoidal rule.

# Arguments
- `T`: the upper bound of the integral (0 is implicitly the lower bound).
        If `T` is negative, `|T|` is used as the lower bound and 0 as the upper bound.

- `r`: the number of time steps.
        Note this is the number of STEPS.
        The number of time POINTS is `r+1`, since they include t=0.

# Returns
- `Ï„`: the length of each time step, simply ``T/r``.
- `Ï„Ì„`: vector of `r+1` time spacings to use as `dt` in integration.
- `tÌ„`: vector of `r+1` time points

The integral ``âˆ«_0^T f(t)â‹…dt`` is evaluated with `sum(f.(tÌ„) .* Ï„Ì„)`.

# Explanation

Intuitively, `Ï„Ì„` is a vector giving `Ï„` for each time point.
But careful! The sum of all `Ï„Ì„` must match the length of the integral, ie. `T`.
But there are `r+1` points, and `(r+1)â‹…Ï„ > T`. How do we reconcile this?
A "Left Hand Sum" would omit `t=T` from the time points;
    a "Right Hand Sum" would omit `t=0`.
The trapezoidal rule omits half a point from each.

That sounds awfully strange, but it's mathematically sound!
We only integrate through *half* of each boundary time point `t=0` and `t=T`.
Thus, those points, and only those points, have a spacing of `Ï„/2`.

In principle, a different grid could be adopted giving more sophisticated quadrature.

"""
@memoize Dict function trapezoidaltimegrid(T::Real, r::Int)
    # NOTE: Negative values of T give reversed time grid.
    Ï„ = T / r
    Ï„Ì„ = fill(Ï„, r+1); Ï„Ì„[[begin, end]] ./= 2
    tÌ„ = abs(Ï„) * (T â‰¥ 0 ? (0:r) : reverse(0:r))
    return Ï„, Ï„Ì„, tÌ„
end


"""
    EvolutionType

Super-type for all evolution algorithms.

# Implementation

Any concrete sub-type `A` must implement the following methods:
- `workbasis(::A)`: which Bases.BasisType the evolution algorithm uses
- `evolve!(::A, device, T, Ïˆ; callback=nothing)`: evolve Ïˆ (in-place) from time 0 to T
                                    (you may assume the basis of Ïˆ is the work basis)

If your evolution algorithm breaks time up into equally-spaced discrete time steps,
    you should implement a `TrotterEvolution`, which has a couple extra requirements.

"""
abstract type EvolutionType end

"""
    workbasis(t::Real)

Which basis the evolution algorithm works in.

Also defines the default basis to interpret Ïˆ as, in evolution methods.

"""
function workbasis(::EvolutionType)
    error("Not Implemented")
    return OCCUPATION
end

"""
    evolve!(evolution, device, [basis, ]T, Ïˆ; basis=nothing, callback=nothing)

Evolve a state `Ïˆ` by time `T` under a `device` Hamiltonian.

This method both mutates and returns `Ïˆ`.

# Arguments
- `evolution::EvolutionType`: which evolution algorithm to use.
- `device::Devices.DeviceType`: specifies which Hamiltonian to evolve under.
- `basis::Bases.BasisType`: which basis `Ïˆ` is represented in.
        Implicitly defaults to `workbasis(evolution)`.
- `T::Real`: the total amount of time to evolve by.
        The evolution is implicitly assumed to start at time `t=0`.
- `Ïˆ`: the initial statevector, defined on the full Hilbert space of the device.

# Keyword Arguments
- `callback`: a function which is called at each iteration of the time evolution.
        The function is passed three arguments:
        - `i`: indexes the iteration
        - `t`: the current time point
        - `Ïˆ`: the current statevector, in the work basis
        The function is called after having evolved Ïˆ into |Ïˆ(t)âŸ©.

"""
function evolve! end

function evolve!(
    evolution::EvolutionType,
    device::Devices.DeviceType,
    T::Real,
    Ïˆ0::AbstractVector;
    callback=nothing,
)
    error("Not Implemented")
    return Ïˆ0
end

function evolve!(
    evolution::EvolutionType,
    device::Devices.DeviceType,
    basis::Bases.BasisType,
    T::Real,
    Ïˆ0::AbstractVector;
    kwargs...
)
    basis == workbasis(evolution) && return evolve!(evolution, device, T, Ïˆ0; kwargs...)

    U = Devices.basisrotation(workbasis(evolution), basis, device)
    Ïˆ0 = LinearAlgebraTools.rotate!(U, Ïˆ0)      # ROTATE INTO WORK BASIS
    Ïˆ0 = evolve!(evolution, device, T, Ïˆ0; kwargs...)
    Ïˆ0 = LinearAlgebraTools.rotate!(U', Ïˆ0)     # ROTATE BACK INTO GIVEN BASIS
    return Ïˆ0
end

"""
    evolve(evolution, device, [basis, ]T, Ïˆ0; result=nothing, kwargs...)

Evolve a state `Ïˆ0` by time `T` under a `device` Hamiltonian, without mutating `Ïˆ0`.

This method simply copies `Ïˆ0` (to `result` if provided, or else to a new array),
    then calls the mutating function `evolve!` on the copy.
Please see `evolve!` for detailed documentation.

"""
function evolve end

function evolve(
    evolution::EvolutionType,
    device::Devices.DeviceType,
    T::Real,
    Ïˆ0::AbstractVector;
    result=nothing,
    kwargs...
)
    F = LinearAlgebraTools.cis_type(Ïˆ0)
    result === nothing && (result = Vector{F}(undef, length(Ïˆ0)))
    result .= Ïˆ0
    return evolve!(evolution, device, T, result; kwargs...)
end

function evolve(
    evolution::EvolutionType,
    device::Devices.DeviceType,
    basis::Bases.BasisType,
    T::Real,
    Ïˆ0::AbstractVector;
    result=nothing,
    kwargs...
)
    F = LinearAlgebraTools.cis_type(Ïˆ0)
    result === nothing && (result = Vector{F}(undef, length(Ïˆ0)))
    result .= Ïˆ0
    return evolve!(evolution, device, basis, T, result; kwargs...)
end



"""
    TrotterEvolution

Super-type for evolution algorithms which divide time into equally-spaced chunks.

This sub-typing facilitates easy comparison between different Trotter algorithms,
    and lets us enforce a consistent time grid
    in the implicilty-Trotterized `gradientsignals` method.

# Implementation

Any concrete sub-type `A` must implement
    *everything* required in the `EvolutionType` interface,
    so consult the documentation for `DeviceType` carefully.

In addition, the following method must be implemented:
- `nsteps(::A)`: the number of Trotter steps

The number of steps will usually be a simple integer field in the implementing struct,
    but this is left as an implementation detail.

"""
abstract type TrotterEvolution <: EvolutionType end

"""
    nsteps(device::DeviceType)

The number of Trotter steps.

"""
function nsteps(::TrotterEvolution)
    error("Not Implemented")
    return 0
end



"""
    gradientsignals(device[, basis], T, Ïˆ0, r, O; kwargs...)

The gradient signals associated with a given `device` Hamiltonian, and an observable `O`.

Gradient signals are used to calculate analytical derivatives of a control pulse.

# Arguments
- `evolution::TrotterEvolution` how to initialize the co-state `|Î»âŸ©`
        Also determines the number of Trotter steps `r` to evaluate ``Ï•_j(t)`` for.
        A standard choice would be `ToggleEvolutions.Toggle(r)`.

- `device::Devices.DeviceType`: specifies which Hamiltonian to evolve under.
        Also identifies each of the gradient operators used to calculate gradient signals.

- `basis::Bases.BasisType`: which basis `Ïˆ` is represented in.
        ALSO determines the basis in which calculations are carried out.
        Defaults to `Bases.OCCUPATION`.

- `T::Real`: the total duration of the pulse.

- `Ïˆ0`: the initial statevector, defined on the full Hilbert space of the device.

- `O`: a Hermitian observable, represented as a matrix.
    Gradients are calculated with respect to the expectation `âŸ¨OâŸ©` at time `T`.

# Keyword Arguments
- `result`: an (optional) pre-allocated array to store gradient signals

- `callback`: a function called at each iteration of the gradient signal calculation.
        The function is passed three arguments:
        - `i`: indexes the iteration
        - `t`: the current time point
        - `Ïˆ`: the current statevector, in the OCCUPATION basis
        The function is called after having evolved Ïˆ into |Ïˆ(t)âŸ©,
            but before calculating Ï•Ì„[i,:]. Evolution here runs backwards.

# Returns
A vector list `Ï•Ì„`, where each `Ï•Ì„[:,j]` is the gradient signal ``Ï•_j(t)``
    evaluated on a time grid given by `trapezoidaltimegrid(T,r)`.


# Explanation
A gradient signal ``Ï•_j(t)`` is defined with respect to a gradient operator ``AÌ‚_j``,
    an observable ``OÌ‚``, a time-dependent state `|Ïˆ(t)âŸ©`, and total pulse duration `T`.

Let us define the expectation value ``E(T) â‰¡ âŸ¨Ïˆ(T)|OÌ‚|Ïˆ(T)âŸ©``.

Define the co-state ``|Î»(t)âŸ©`` as the (un-normalized) statevector
    which satisfies ``E(T)=âŸ¨Î»(t)|Ïˆ(t)âŸ©`` for any time `tâˆŠ[0,T]`.
The gradient signal is defined as ``Ï•_j(t) â‰¡ âŸ¨Î»(t)|(iAÌ‚_j)|Ïˆ(t)âŸ© + h.t.``.


    gradientsignals(device[, basis], T, Ïˆ0, r, OÌ„; kwargs...)

When the matrix argument `O` is replaced by a matrix list `OÌ„`,
    each `OÌ„[:,:,k]` represents a different Hermitian observable ``OÌ‚_k``.
In this case, a different set of gradient signals is computed for *each* ``OÌ‚_k``.

# Returns
A 3d array `Ï•Ì„`, where each `Ï•Ì„[:,j,k]` is the gradient signal ``Ï•_j(t)``
    defined with respect to the observable ``OÌ‚_k``.

# Explanation
Multiple sets of gradient signals may be useful
    if you want to compute gradients with respect to multiple observables.
For example, gradients with respect to a normalized molecular energy
    include contributions from both a molecular Hamiltonian and a leakage operator.
This method enables such calculations using only a single "pass" through time.

"""
function gradientsignals(
    evolution::TrotterEvolution,
    device::Devices.DeviceType,
    args...;
    kwargs...
)
    return gradientsignals(evolution, device, workbasis(evolution), args...; kwargs...)
end

function gradientsignals(
    evolution::TrotterEvolution,
    device::Devices.DeviceType,
    basis::Bases.BasisType,
    T::Real,
    Ïˆ0::AbstractVector,
    O::AbstractMatrix;
    result=nothing,
    kwargs...
)
    # `O` AND `result` GIVEN AS 2D ARRAYS BUT MUST BE 3D FOR DELEGATION
    result !== nothing && (result = reshape(result, size(result)..., 1))
    OÌ„ = reshape(O, size(O)..., 1)

    # PERFORM THE DELEGATION
    result = gradientsignals(evolution, device, basis, T, Ïˆ0, OÌ„; result=result, kwargs...)

    # NOW RESHAPE `result` BACK TO 2D ARRAY
    result = reshape(result, size(result, 1), size(result, 2))
    return result
end

function gradientsignals(
    evolution::TrotterEvolution,
    device::Devices.DeviceType,
    basis::Bases.BasisType,
    T::Real,
    Ïˆ0::AbstractVector,
    OÌ„::LinearAlgebraTools.MatrixList;
    result=nothing,
    callback=nothing,
)
    r = nsteps(evolution)
    Ï„, Ï„Ì„, tÌ„ = trapezoidaltimegrid(T, r)

    # PREPARE SIGNAL ARRAYS Ï•Ì„[i,j,k]
    if result === nothing
        F = real(LinearAlgebraTools.cis_type(Ïˆ0))
        result = Array{F}(undef, r+1, Devices.ngrades(device), size(OÌ„,3))
    end

    # PREPARE STATE AND CO-STATES
    ÏˆTYPE = LinearAlgebraTools.cis_type(Ïˆ0)
    Ïˆ = array(ÏˆTYPE, size(Ïˆ0), LABEL); Ïˆ .= Ïˆ0
    Ïˆ = evolve!(evolution, device, basis, T, Ïˆ)

    Î»Ì„ = array(ÏˆTYPE, (size(Ïˆ0,1), size(OÌ„,3)), LABEL)
    for k in axes(OÌ„,3)
        Î»Ì„[:,k] .= Ïˆ
        LinearAlgebraTools.rotate!(@view(OÌ„[:,:,k]), @view(Î»Ì„[:,k]))
    end

    # ROTATE INTO OCCUPATION BASIS FOR THE REST OF THIS METHOD
    if basis != OCCUPATION
        U = Devices.basisrotation(OCCUPATION, basis, device)
        Ïˆ = LinearAlgebraTools.rotate!(U, Ïˆ)
        for k in axes(OÌ„,3)
            LinearAlgebraTools.rotate!(U, @view(Î»Ì„[:,k]))
        end
    end

    # LAST GRADIENT SIGNALS
    callback !== nothing && callback(r+1, tÌ„[r+1], Ïˆ)
    for k in axes(OÌ„,3)
        Î» = @view(Î»Ì„[:,k])
        for j in 1:Devices.ngrades(device)
            z = Devices.braket(Gradient(j, tÌ„[end]), device, OCCUPATION, Î», Ïˆ)
            result[r+1,j,k] = 2 * imag(z)   # Ï•Ì„[i,j,k] = -ð‘–z + ð‘–zÌ„
        end
    end

    # ITERATE OVER TIME
    for i in reverse(1:r)
        # COMPLETE THE PREVIOUS TIME-STEP AND START THE NEXT
        Ïˆ = Devices.propagate!(Drive(tÌ„[i+1]), device, OCCUPATION, -Ï„/2, Ïˆ)
        Ïˆ = Devices.propagate!(STATIC, device, OCCUPATION, -Ï„, Ïˆ)
        Ïˆ = Devices.propagate!(Drive(tÌ„[i]),   device, OCCUPATION, -Ï„/2, Ïˆ)
        for k in axes(OÌ„,3)
            Î» = @view(Î»Ì„[:,k])
            Devices.propagate!(Drive(tÌ„[i+1]), device, OCCUPATION, -Ï„/2, Î»)
            Devices.propagate!(STATIC, device, OCCUPATION, -Ï„, Î»)
            Devices.propagate!(Drive(tÌ„[i]),   device, OCCUPATION, -Ï„/2, Î»)
        end

        # CALCULATE GRADIENT SIGNAL BRAKETS
        callback !== nothing && callback(i, tÌ„[i], Ïˆ)
        for k in axes(OÌ„,3)
            Î» = @view(Î»Ì„[:,k])
            for j in 1:Devices.ngrades(device)
                z = Devices.braket(Gradient(j, tÌ„[i]), device, OCCUPATION, Î», Ïˆ)
                result[i,j,k] = 2 * imag(z) # Ï•Ì„[i,j,k] = -ð‘–z + ð‘–zÌ„
            end
        end
    end

    return result
end

